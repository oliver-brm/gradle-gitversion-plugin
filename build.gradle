plugins {
    id 'java'
    id 'groovy'
    id 'jvm-test-suite'
    id 'com.gradle.plugin-publish' version '1.0.0-rc-2'
}

repositories {
    mavenCentral()
}

group = 'org.hypergammaspace.gradle-plugins'
version = '0.11.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            configurations {
                integrationTestImplementation.extendsFrom implementation
            }
            dependencies {
                implementation project
                implementation 'org.junit.jupiter:junit-jupiter'
                implementation('org.spockframework:spock-core:2.1-groovy-3.0') {
                    exclude module: 'groovy-all'
                }
            }
        }
    }
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.1.0.202203080745-r'
    implementation 'com.google.guava:guava:31.1-android'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation platform('org.junit:junit-bom:5.8.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation('org.spockframework:spock-core:2.1-groovy-3.0') {
        exclude module: 'groovy-all'
    }

}


gradlePlugin {

    plugins {
        gitVersionPlugin {
            id = 'org.hypergammaspace.gitversion'
            displayName = friendly_name
            implementationClass = 'org.unbrokendome.gradle.plugins.gitversion.GitVersionPlugin'
        }
    }

    testSourceSets project.sourceSets.integrationTest
}

apply from: 'publishing.gradle'
